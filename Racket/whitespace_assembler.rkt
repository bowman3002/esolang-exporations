#lang racket

(require "whitespace_converter.rkt")

(define (number->whitespacechar number)
  (define (binary->whitespacechar binary)
    (if (= binary 0)
        #\s
        #\t))
  (define (number->whitespacechar-rec number char-list)
    (if (= number 0)
        (append char-list '(#\n))
        (number->whitespacechar-rec (quotient number 2) (cons (binary->whitespacechar (remainder number 2)) char-list))))
  (if (< number 0)
      (cons #\t (number->whitespacechar-rec (abs number) '()))
      (cons #\s (number->whitespacechar-rec number '()))))

(define (assembly->whitespacechars command)
  (cond ((equal? command "dup")
         '(#\s #\n #\s))
        ((equal? command "swp")
         '(#\s #\n #\t))
        ((equal? command "dis")
         '(#\s #\n #\n))
        ((equal? command "add")
         '(#\t #\s #\s #\s))
        ((equal? command "sub")
         '(#\t #\s #\s #\t))
        ((equal? command "mul")
         '(#\t #\s #\s #\n))
        ((equal? command "div")
         '(#\t #\s #\t #\s))
        ((equal? command "mod")
         '(#\t #\s #\t #\t))
        ((equal? command "str")
         '(#\t #\t #\s))
        ((equal? command "rtr")
         '(#\t #\t #\t))
        ((equal? command "ret")
         '(#\n #\t #\n))
        ((equal? command "end")
         '(#\n #\n #\n))
        ((equal? command "pch")
         '(#\t #\n #\s #\s))
        ((equal? command "pnm")
         '(#\t #\n #\s #\t))
        ((equal? command "rch")
         '(#\t #\n #\t #\s))
        ((equal? command "rnm")
         '(#\t #\n #\t #\t))
        (else
         (assembly-arg->whitespacechars command))))

(define (assembly-arg->whitespacechars command-with-arg)
  (let* ((split-command (string-split command-with-arg " "))
         (command (list-ref split-command 0))
         (arg (number->whitespacechar (string->number (list-ref split-command 1)))))
    (cond ((equal? command "lbl")
           `(#\n #\s #\s . ,arg))
          ((equal? command "cal")
           `(#\n #\s #\t . ,arg))
          ((equal? command "jmp")
           `(#\n #\s #\n . ,arg))
          ((equal? command "jez")
           `(#\n #\t #\s . ,arg))
          ((equal? command "jng")
           `(#\n #\t #\t . ,arg))
          ((equal? command "psh")
           `(#\s #\s . ,arg))
          (else #f))))

(define (read-assembly-from-file filename process)
  (define in (open-input-file filename))
  (define (process-line port process rev-lists)
    (let ((line (read-line port)))
      (if (eq? line eof)
          (begin
            (close-input-port in)
            rev-lists)
          (process-line port process (append rev-lists (process line))))))
  (process-line in process '()))

(define (assembly->whitespace filename)
  (run-from-data (read-assembly-from-file filename assembly->whitespacechars)))